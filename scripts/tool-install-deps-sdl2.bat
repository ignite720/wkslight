@echo off
setlocal enabledelayedexpansion

cd ..

set SDL2_NAME=SDL2
set SDL2_IMAGE_NAME=SDL2_image
set SDL2_MIXER_NAME=SDL2_mixer
set SDL2_NET_NAME=SDL2_net
set SDL2_TTF_NAME=SDL2_ttf

set SDL2_DIST_VERSION=2.32.4
set SDL2_IMAGE_DIST_VERSION=2.8.8
set SDL2_MIXER_DIST_VERSION=2.8.1
set SDL2_NET_DIST_VERSION=2.2.0
set SDL2_TTF_DIST_VERSION=2.24.0

set SDL2_DEV=devel
set SDL2_ARCH=x64
set SDL2_DIST_SUFFIX=VC.zip

set SDL2_DIST_FILENAME=%SDL2_NAME%-%SDL2_DEV%-%SDL2_DIST_VERSION%-%SDL2_DIST_SUFFIX%
set SDL2_IMAGE_DIST_FILENAME=%SDL2_IMAGE_NAME%-%SDL2_DEV%-%SDL2_IMAGE_DIST_VERSION%-%SDL2_DIST_SUFFIX%
set SDL2_MIXER_DIST_FILENAME=%SDL2_MIXER_NAME%-%SDL2_DEV%-%SDL2_MIXER_DIST_VERSION%-%SDL2_DIST_SUFFIX%
set SDL2_NET_DIST_FILENAME=%SDL2_NET_NAME%-%SDL2_DEV%-%SDL2_NET_DIST_VERSION%-%SDL2_DIST_SUFFIX%
set SDL2_TTF_DIST_FILENAME=%SDL2_TTF_NAME%-%SDL2_DEV%-%SDL2_TTF_DIST_VERSION%-%SDL2_DIST_SUFFIX%

set SDL2_DIST_URL_PART1_ARR[0]=SDL
set SDL2_DIST_URL_PART1_ARR[1]=SDL_image
set SDL2_DIST_URL_PART1_ARR[2]=SDL_mixer
set SDL2_DIST_URL_PART1_ARR[3]=SDL_net
set SDL2_DIST_URL_PART1_ARR[4]=SDL_ttf

set SDL2_DIST_URL_PART2_ARR[0]=%SDL2_DIST_VERSION%
set SDL2_DIST_URL_PART2_ARR[1]=%SDL2_IMAGE_DIST_VERSION%
set SDL2_DIST_URL_PART2_ARR[2]=%SDL2_MIXER_DIST_VERSION%
set SDL2_DIST_URL_PART2_ARR[3]=%SDL2_NET_DIST_VERSION%
set SDL2_DIST_URL_PART2_ARR[4]=%SDL2_TTF_DIST_VERSION%

set SDL2_DIST_URL_PART3_ARR[0]=%SDL2_DIST_FILENAME%
set SDL2_DIST_URL_PART3_ARR[1]=%SDL2_IMAGE_DIST_FILENAME%
set SDL2_DIST_URL_PART3_ARR[2]=%SDL2_MIXER_DIST_FILENAME%
set SDL2_DIST_URL_PART3_ARR[3]=%SDL2_NET_DIST_FILENAME%
set SDL2_DIST_URL_PART3_ARR[4]=%SDL2_TTF_DIST_FILENAME%

set SDL2_DIST_DIRNAME_ARR[0]=%SDL2_NAME%-%SDL2_DIST_VERSION%
set SDL2_DIST_DIRNAME_ARR[1]=%SDL2_IMAGE_NAME%-%SDL2_IMAGE_DIST_VERSION%
set SDL2_DIST_DIRNAME_ARR[2]=%SDL2_MIXER_NAME%-%SDL2_MIXER_DIST_VERSION%
set SDL2_DIST_DIRNAME_ARR[3]=%SDL2_NET_NAME%-%SDL2_NET_DIST_VERSION%
set SDL2_DIST_DIRNAME_ARR[4]=%SDL2_TTF_NAME%-%SDL2_TTF_DIST_VERSION%

set /a SDL2_DIST_LEN=5
set /a SDL2_DIST_LAST=%SDL2_DIST_LEN%-1
set /a SDL2_DIST_FILELIST_ALL_EXIST=1

set "SDL2_DIST_FILELIST="
for /L %%i in (0,1,%SDL2_DIST_LEN%) do (
    set "SDL2_DIST_FILELIST=!SDL2_DIST_FILELIST! !SDL2_DIST_URL_PART3_ARR[%%i]!"
)
set "SDL2_DIST_FILELIST=%SDL2_DIST_FILELIST:~1,-1%"

set OPT_DIRNAME=opt
set OPT_TEMP_DIRNAME=temp
set BINARY_DIR=../bin/target/%SDL2_ARCH%/Release

mkdir %OPT_DIRNAME%
pushd %OPT_DIRNAME%

for %%i in (%SDL2_DIST_FILELIST%) do (
    if not exist %%i (
        set /a SDL2_DIST_FILELIST_ALL_EXIST=0
        goto BREAK1
    )
)

:BREAK1
echo SDL2_DIST_FILELIST_ALL_EXIST=!SDL2_DIST_FILELIST_ALL_EXIST!

if %SDL2_DIST_FILELIST_ALL_EXIST% equ 0 (
    for /L %%i in (0,1,%SDL2_DIST_LAST%) do (
        curl -LO "https://github.com/libsdl-org/!SDL2_DIST_URL_PART1_ARR[%%i]!/releases/download/release-!SDL2_DIST_URL_PART2_ARR[%%i]!/!SDL2_DIST_URL_PART3_ARR[%%i]!"
    )
)

if not exist %OPT_TEMP_DIRNAME% (
    mkdir %OPT_TEMP_DIRNAME%
    
    for /L %%i in (0,1,%SDL2_DIST_LAST%) do (
        powershell -Command "Expand-Archive -Force -LiteralPath !SDL2_DIST_URL_PART3_ARR[%%i]! -DestinationPath %OPT_TEMP_DIRNAME%"
    )
)

for /L %%i in (0,1,%SDL2_DIST_LAST%) do (
    xcopy /I /Y "%OPT_TEMP_DIRNAME%\!SDL2_DIST_DIRNAME_ARR[%%i]!\lib\%SDL2_ARCH%\*.dll" "%BINARY_DIR%"
)

popd